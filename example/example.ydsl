module Example;

import "versioned.ydsl";

class Named {
    name Text check nonEmpty;
    unique Name name;
}

entity User {
    instance of Named, Versioned;
    firstName Text;
    lastName Text;
    groupId Maybe GroupId;
    deriving Typeable;
}

entity Group {
    instance of Named;
}

entity BlogPost {
    instance of Named;
    authorId UserId;
    content Text;
}
enum CommentState = Pending | Accepted | Spam;

entity Comment {
    blogPostId BlogPostId;
    authorId UserId;
    comment Text;
    time ZonedTime;
    commentState CommentState;
    check maxTwoPendingComments;
}

route /users {
    get {
        select p.id, p.* from User as p
            where p.groupId in (select g.id from Group as g)
            order by p.name asc
            limit 1000;
        default-filter-sort;
    }
    post {
        insert User;
    }
}

route /user/#UserId {
    get {
        select p.* from User as p where p.id = $1;
    }
    put {
        update User identified by $1;
    }
    delete {
        delete from User as p where p.id = $1;
    }
}

route /blogposts {
    get {
        public;
        select bp.id, bp.*, p.name as authorName 
            from BlogPost as bp
            inner join User as p on p.id = bp.authorId
            order by bp.name asc
            limit 1000;

        if param "blogPostName" = $$ then
            where bp.name like "%" || $$ || "%";
        default-filter-sort;
    }
    post {
        insert BlogPost;
    }
}

route /blogposts/#BlogPostId {
    get {
        public;
        select bp.*, p.name as authorName from BlogPost as bp 
            inner join User as p on p.id = bp.authorId
            where bp.id = $1;
    }
    put {
        update BlogPost identified by $1;
    }
    delete {
        delete from BlogPost as bp where bp.id = $1;
    }
}

route /comments {
    get {
        public;
        select bp.id, bp.* from BlogPost as bp
            order by bp.name asc
            limit 1000;

        if param "authorId" = $$ then
            inner join User as p on bp.authorId = p.id
            where p.id = $$;

        if param "query" = $$ then
            where bp.content ilike "%" || $$ || "%";

        default-filter-sort;
    }
    post {
        insert BlogPost;
    }
}

route /comments/#CommentId {
    get {
        public;
        select c.* from Comment as c where c.id = $1;
    }
    put {
        update Comment identified by $1;
    }
    delete {
        delete from Comment as c where c.id = $1;
    }
}
